plugins {
    id 'nu.studer.jooq' version '4.2'
}

apply plugin: "java"

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets.main.java.srcDirs = [ "src" ]
sourceSets.test.java.srcDirs = [ "test" ]

dependencies {
    jooqRuntime 'org.xerial:sqlite-jdbc:3.30.1'
    jooqRuntime 'org.jooq:jooq-meta'
    jooqRuntime 'org.jooq:jooq-meta-extensions'
    jooqRuntime "org.jooq:jooq-codegen"
}

jooq {
    version = '3.13.5'
    edition = 'OSS'
    generateSchemaSourceOnCompilation = true
    template(sourceSets.main) {
        logging = org.jooq.meta.jaxb.Logging.WARN
        jdbc {
            driver = 'org.sqlite.JDBC'
            url = 'jdbc:sqlite:template.sqlite'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                name = 'org.jooq.meta.sqlite.SQLiteDatabase'
                includes = '.*'
                excludes = 'sqlite_.*'
            }
            target {
                packageName = 'net.sync.game.database.schema'
                directory = 'src'
            }
        }
    }
}

//Enforces generation of the schema sources when task is called
generateTemplateJooqSchemaSource.outputs.upToDateWhen { false }
//Disable generation of the schema on compilation
project.tasks.getByName('compileJava').dependsOn -= 'generateTemplateJooqSchemaSource'
